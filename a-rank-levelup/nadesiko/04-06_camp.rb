# 陣取りゲーム (paizaランク A 相当)
# https://paiza.jp/works/mondai/a_rank_level_up_problems/a_rank_camp_boss

exec "cnako3", "-e", <<敬具
●（入力文字列の）メイン処理とは
　　### 入力処理 ###
　　入力行配列＝入力文字列をトリムして改行で区切る
　　Ｈ、Ｗ＝入力行配列【０】を「 」で区切る
　　先攻＝入力行配列【１】
　　盤面配列＝入力行配列の２からＨを配列取り出し
　　Ａ＝｛「記号」：「A」、「陣地数」：０、「境界陣地」：【】、「継続」：はい｝
　　Ｂ＝｛「記号」：「B」、「陣地数」：０、「境界陣地」：【】、「継続」：はい｝
　　行番号で０からＨ－１まで繰り返す
　　　　盤面配列【行番号】＝盤面配列【行番号】を「」で区切る
　　　　列番号で０からＷ－１まで繰り返す
　　　　　　もし（盤面配列【行番号】【列番号】＝「A」）なら
　　　　　　　　Ａ＠「陣地数」＝Ａ＠「陣地数」＋１
　　　　　　　　【行番号、列番号】をＡ＠「境界陣地」に配列追加
　　　　　　違えばもし（盤面配列【行番号】【列番号】＝「B」）なら
　　　　　　　　Ｂ＠「陣地数」＝Ｂ＠「陣地数」＋１
　　　　　　　　【行番号、列番号】をＢ＠「境界陣地」に配列追加
　　　　　　ここまで
　　　　ここまで
　　ここまで

　　### 先攻・後攻の設定 ###
　　順番＝【Ａ、Ｂ】
　　もし（先攻＝「B」）なら
　　　　順番＝【Ｂ、Ａ】
　　ここまで

　　### 設定 ###
　　ベクトル＝【【－１、０】、【０、１】、【１、０】、【０、－１】】
　　ベクトル数＝ベクトルの配列要素数
　　参加人数＝順番の配列要素数
　　ターン＝０
　　### ループ処理 ###
　　継続＝参加人数
　　（継続）の間繰り返す
　　　　プレイヤー＝順番【ターン％参加人数】
　　　　ターンを１増やす
　　　　
　　　　### 陣地を増やせないプレイヤーはスキップ ###
　　　　もし（プレイヤー＠「継続」＝いいえ）なら、続ける
　　　　もし（（プレイヤー＠「境界陣地」の配列要素数）＝０）なら
　　　　　　プレイヤー＠「継続」＝いいえ
　　　　　　継続を１減らす
　　　　　　続ける
　　　　ここまで

　　　　### 境界陣地の隣接マスを調べる ###
　　　　新陣地＝｛｝
　　　　現在地でプレイヤー＠「境界陣地」を反復
　　　　　　現在Ｙ、現在Ｘ＝現在地
　　　　　　方向でベクトルを反復
　　　　　　　　差分Ｙ、差分Ｘ＝方向
　　　　　　　　次Ｙ＝現在Ｙ＋差分Ｙ
　　　　　　　　次Ｘ＝現在Ｘ＋差分Ｘ
　　　　　　　　もし（新陣地＠【次Ｙ、次Ｘ】）なら、続ける

　　　　　　　　### 盤面外と障害物判定 ###
　　　　　　　　もし（次Ｙ＜０）または（次Ｙ＞＝Ｈ）なら、続ける
　　　　　　　　もし（次Ｘ＜０）または（次Ｘ＞＝Ｗ）なら、続ける
　　　　　　　　もし（盤面配列【次Ｙ】【次Ｘ】＜＞「.」）なら、続ける
　　　　　　　　
　　　　　　　　### 新しい陣地 ###
　　　　　　　　盤面配列【次Ｙ】【次Ｘ】＝プレイヤー＠「記号」
　　　　　　　　新陣地＠【次Ｙ、次Ｘ】＝はい
　　　　　　ここまで
　　　　ここまで
　　　　プレイヤー＠「境界陣地」＝【】
　　　　新陣地を反復
　　　　　　プレイヤー＠「陣地数」＝プレイヤー＠「陣地数」＋１
　　　　　　Ｙ、Ｘ＝対象キーを「,」で区切る
　　　　　　【Ｙ、Ｘ】をプレイヤー＠「境界陣地」に配列追加
　　　　ここまで
　　ここまで

　　### 結果（必ずゲームの勝者が決定する） ###
　　勝者＝Ａ＠「記号」
　　もし（Ａ＠「陣地数」＜Ｂ＠「陣地数」）なら
　　　　勝者＝Ｂ＠「記号」
　　ここまで
　　「｛Ａ＠『陣地数』｝ ｛Ｂ＠『陣地数』｝｛改行｝｛勝者｝」で戻る
ここまで

拝啓
「/dev/stdin」から開いて入力文字列に代入してください。
入力文字列のメイン処理を表示してください。お願いします。
敬具
